#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ПриОпределенииНастроекПечати(НастройкиОбъекта) Экспорт	
	НастройкиОбъекта.ПриДобавленииКомандПечати = Истина;
КонецПроцедуры

Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АнкетаКлиента";
	КомандаПечати.Представление = НСтр("ru = 'Анкета клиента'");
	КомандаПечати.Порядок = 5;
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ТранспортнаяНакладная";
	КомандаПечати.Представление = НСтр("ru = 'Транспортная накладная'");
	КомандаПечати.Порядок = 10;
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АнкетаКлиента,ТранспортнаяНакладная";
	КомандаПечати.Представление = НСтр("ru = 'Комплект документов'");
	КомандаПечати.Порядок = 75;
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "УправлениеПечатью";
	КомандаПечати.Идентификатор = "Документ.Д_Доставка.ПФ_DOC_Договор";
	КомандаПечати.Представление = НСтр("ru = 'Договор в Word'");
	КомандаПечати.Порядок = 80;
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "АнкетаКлиента");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = ПечатьАнкеты(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Анкета клиента'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Документ.Д_Доставка.ПФ_MXL_АнкетаКлиента";
	КонецЕсли;
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "ТранспортнаяНакладная");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = ПечатьТранспортнойНакладной(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Транспортная накладная'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Документ.Д_Доставка.ПФ_MXL_ТранспортнаяНакладная";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПечатьАнкеты(МассивОбъектов, ОбъектыПечати)
	
	ТабДок = Новый ТабличныйДокумент;
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.Д_Доставка.ПФ_MXL_АнкетаКлиента");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Д_Доставка.Ссылка КАК Ссылка,
	|	Д_Доставка.Дата КАК Дата,
	|	Д_Доставка.Номер КАК Номер
	|ИЗ
	|	Документ.Д_Доставка КАК Д_Доставка
	|ГДЕ
	|	Д_Доставка.Ссылка В(&МассивОбъектов)";
	
	Запрос.Параметры.Вставить("МассивОбъектов", МассивОбъектов);
	Выборка = Запрос.Выполнить().Выбрать();
	
	ОбластьАнкета = Макет.ПолучитьОбласть("Анкета");
	
	ВставлятьРазделительСтраниц = Ложь;
	Пока Выборка.Следующий() Цикл
		Если ВставлятьРазделительСтраниц Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
		
		ОбластьАнкета.Параметры.Номер = Выборка.Номер;
		ОбластьАнкета.Параметры.Дата = Формат(Выборка.Дата, "ДФ=dd.MM.yyyy");
		
		ВставитьQRКодСоСсылкой(Выборка.Ссылка, ОбластьАнкета.Рисунки.QRКод.Картинка);
        	
		ТабДок.Вывести(ОбластьАнкета);
		
		ВставлятьРазделительСтраниц = Истина;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало, ОбъектыПечати, Выборка.Ссылка);		
		
	КонецЦикла;	
		
	Возврат ТабДок;
	
КонецФункции

Функция ПечатьТранспортнойНакладной(МассивОбъектов, ОбъектыПечати)
	
	ТабДок = Новый ТабличныйДокумент;
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.Д_Доставка.ПФ_MXL_ТранспортнаяНакладная");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Д_Доставка.Ссылка КАК Ссылка,
	|	Д_Доставка.Дата КАК Дата,
	|	Д_Доставка.Контрагент КАК Грузополучатель,
	|	Д_Доставка.Номер КАК Номер,
	|	Д_Доставка.Организация КАК Грузоотправитель,
	|	Д_Доставка.Товары.(
	|		НомерСтроки КАК НомерСтроки,
	|		Номенклатура КАК Товар,
	|		Количество КАК Количество
	|	) КАК Товары
	|ИЗ
	|	Документ.Д_Доставка КАК Д_Доставка
	|ГДЕ
	|	Д_Доставка.Ссылка В(&МассивОбъектов)";
	
	Запрос.Параметры.Вставить("МассивОбъектов", МассивОбъектов);
	Выборка = Запрос.Выполнить().Выбрать();

	Шапка = Макет.ПолучитьОбласть("Шапка");
	ТаблицаШапка = Макет.ПолучитьОбласть("ТаблицаШапка");
	ТаблицаСтрока = Макет.ПолучитьОбласть("ТаблицаСтрока");
	Подвал = Макет.ПолучитьОбласть("Подвал");

	ВставлятьРазделительСтраниц = Ложь;
	Пока Выборка.Следующий() Цикл
		Если ВставлятьРазделительСтраниц Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
		
		Шапка.Параметры.Заполнить(Выборка);
		Шапка.Параметры.Дата = Формат(Выборка.Дата, "ДФ=dd.MM.yyyy"); 
		
		ВставитьQRКодСоСсылкой(Выборка.Ссылка, Шапка.Рисунки.QRКод.Картинка);
		
		ТабДок.Вывести(Шапка);

		ТабДок.Вывести(ТаблицаШапка);
		
		ВыборкаТовары = Выборка.Товары.Выбрать();
		Пока ВыборкаТовары.Следующий() Цикл
			ТаблицаСтрока.Параметры.Заполнить(ВыборкаТовары);
			ТабДок.Вывести(ТаблицаСтрока);
		КонецЦикла;

		ТабДок.Вывести(Подвал);

		ВставлятьРазделительСтраниц = Истина;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало, ОбъектыПечати, Выборка.Ссылка);		

	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции

Процедура ВставитьQRКодСоСсылкой(ДокументСсылка, СвойствоКартинка)
	
	ДанныеQRКода = ГенерацияШтрихкода.ДанныеQRКода(ПолучитьНавигационнуюСсылку(ДокументСсылка), 1, 120);
	Если Не ТипЗнч(ДанныеQRКода) = Тип("ДвоичныеДанные") Тогда
		ТекстСообщения = НСтр("ru = 'Не удалось сформировать QR-код.
		|Технические подробности см. в журнале регистрации.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		СвойствоКартинка = Новый Картинка;
	Иначе
		КартинкаQRКода = Новый Картинка(ДанныеQRКода);
		СвойствоКартинка = КартинкаQRКода;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецЕсли